.program bl_pwm
.side_set 1 opt

    pull noblock    side 0 ; Pull from FIFO to OSR if available, else copy X to OSR.
    mov x, osr             ; Copy most-recently-pulled value back to scratch X
    mov y, isr             ; ISR contains PWM period. Y used as counter.
countloop:
    jmp x!=y noset         ; Set pin high if X == Y, keep the two paths length matched
    jmp skip        side 1
noset:
    nop                    ; Single dummy cycle to keep the two paths the same length
skip:
    jmp y-- countloop      ; Loop until Y hits 0, then pull a fresh PWM value from FIFO

% c-sdk {
static inline void bl_pwm_program_init(PIO pio, uint sm, uint offset, uint pin) {
   pio_gpio_init(pio, pin);
   pio_sm_set_consecutive_pindirs(pio, sm, pin, 1, true);
   pio_sm_config c = bl_pwm_program_get_default_config(offset);
   sm_config_set_clkdiv(&c, 200.0f);
   sm_config_set_sideset_pins(&c, pin);
   pio_sm_init(pio, sm, offset, &c);
}
%}



.program lcd_rx
.side_set 1
.in 1 left auto 8

reset:
    pull block      side 0
    mov x, osr      side 0
.wrap_target
    jmp x--, jump   side 0 
    jmp reset       side 1
jump:
    in pins, 1      side 1
.wrap

.program lcd_tx_double
.side_set 1 opt
.out 1 left 32

; Thank you Pimoroni Picosystem
; from their "screen_double.pio" file

.wrap_target

  pull                      ; fetch two pixels
  out x, 16                 ; first pixel in x register
  out y, 16                 ; second pixel in y register

; write first pixel

  mov osr, x                ; move pixel data back into osr from register x
  out null, 16              ; discard the 16-bits of dummy data + 4-bits of alpha we don't need from the mov
p1:
  out pins, 1   side 0      ; output bit, clear clock
  jmp !osre p1  side 1      ; if still bits in osr then jump back to start of loop and clock in data

; duplicate first pixel

  mov osr, x                ; move pixel data back into osr from register x
  out null, 16              ; discard as above..
p1d:
  out pins, 1   side 0      ; output bit, clear clock
  jmp !osre p1d  side 1     ; if still bits in osr then jump back to start of loop and clock in data

; write second pixel

  mov osr, y                ; move pixel data back into osr from register y
  out null, 16              ; discard as above..
p2:
  out pins, 1   side 0      ; output bit, clear clock
  jmp !osre p2  side 1      ; if still bits in osr then jump back to start of loop and clock in data

; duplicate second pixel

  mov osr, y                ; move pixel data back into osr from register y
  out null, 16              ; discard as above..
p2d:
  out pins, 1   side 0      ; output bit, clear clock
  jmp !osre p2d  side 1     ; if still bits in osr then jump back to start of loop and clock in data

.wrap

